buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
    }
}

plugins {
    id "com.jfrog.bintray" version "1.6"
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group = 'com.bertramlabs.plugins'
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")



repositories {
    mavenLocal()
    jcenter()
	mavenCentral()
}

configurations {
    provided
}
 

dependencies {
    compile("commons-logging:commons-logging:1.1.1")
    compile('com.bertramlabs.plugins:hcl4j:0.2.2')
    testCompile "org.codehaus.groovy:groovy-all:2.4.1"
    testCompile "org.spockframework:spock-core:0.7-groovy-2.0"
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId 'scribe-core'
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'scribe-core'
                    description 'scribe provides secure secret management with triple level encryption and pluggable backends.'
                    url 'https://github.com/gomorpheus/scribe'
                    scm {
                        url 'https://github.com/gomorpheus/scribe'
                        connection 'scm:https://gomorpheus@github.com/gomorpheus/scribe.git'
                        developerConnection 'scm:git://github.com/gomorpheus/scribe.git'
                    }
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'davydotcom'
                            name 'David Estes'
                            email 'davydotcom@gmail.com'
                        }
                        developer {
                            id 'bdwheeler'
                            name 'Brian Wheeler'
                            email 'brian@morpheusdata.com'
                        }
                    }
                }
            }
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

sourceSets {
    main {
        compileClasspath += configurations.provided
    }
}

//Define bintrayUser and bintrayKey in ~/.gradle/gradle.properties
bintray {
    if(project.hasProperty('bintrayUser')) {
        user = bintrayUser
        key = bintrayKey
    }
    publications = ['maven']
    pkg {
        repo = 'gomorpheus'
        userOrg = 'bertramlabs'
        name = 'scribe-core'
        vcsUrl = 'https://github.com/gomorpheus/scribe.git'
        licenses = ['Apache-2.0']
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task morpheusJavaDoc(type: Javadoc) {
  source = sourceSets.main.allJava
  title = "Scribe Core Docs"
}

task(console, dependsOn: 'classes', type: JavaExec) {
  main = 'groovy.ui.Console'
  classpath = sourceSets.main.runtimeClasspath
}

test {
    testLogging {
        exceptionFormat = 'full'
        showStandardStreams = true
    }
}


bintrayUpload.dependsOn build, sourcesJar, javadocJar
